class Dinosaur {
    field int x, y; // Position of the dinosaur
    field boolean isJumping, isFalling;

    field Ground ground;

    field int jumpHeight;

    constructor Dinosaur new(int Ax, int Ay, int groundPosition) {
        let x = Ax;
        let y = Ay;
        let jumpHeight = 60;
        let ground = Ground.new(groundPosition);

        return this;
    }

    method void run(){
        do jumping();
        do falling();

        do drawDino();
        do ground.drawGround();

        do Sys.wait(5); 

        return;
    }

    
    method void startJump() {
        if (~isFalling){
            let isJumping = true;
        }
        return;
    }

    method void jumping(){
        if (isJumping){
            do clearDino();
            let y = y - 1;
            if (y < jumpHeight){
                do startFall();
            }
        }
        return;
    }

    method void startFall(){
        let isJumping = false;
        let isFalling = true;
        return;
    }

    method void falling(){
        if (isFalling){
            do clearDino();
            let y = y + 1;
            if ((y + sizeY()) = ground.groundPosition()){
                do stopFall();
            }
        }
        return;
    }

    method void stopFall(){
        let isFalling = false;
        return;
    }

    method void drawDino(){
        do Screen.setColor(true);
        if ((x > 0) & (x < 506) & (y > 10) & (y < 255))
        {
            do Screen.drawRectangle(x, y, x + sizeX(), y + sizeY());
        }
        return;
    }

    method void clearDino(){
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + sizeX(), y + sizeY());
        return;
    }

    method int sizeX(){
        return 40;
    }
    method int sizeY(){
        return 60;
    }

}
